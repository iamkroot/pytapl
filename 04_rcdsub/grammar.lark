start: (command ";")*

?command: term
        | CNAME ":" func_ty                  -> bind

?term: app_term
     | "lambda" CNAME ":" func_ty "." term   -> abs

?path_term: path_term "." CNAME              -> proj
          | aterm

?app_term: path_term
         | app_term path_term                -> app

?aterm: "(" term ")"
      | CNAME                                -> var
      | "{" rcd_fields "}"                 

rcd_fields: (rcd_field ",")* rcd_field       -> record

rcd_field: CNAME "=" term

?func_ty: type "->" func_ty                  -> arr_ty 
        | "{" field_types "}"
        | type

field_types: (field_ty ",")* field_ty        -> rcd_ty

field_ty: CNAME ":" func_ty

?type: "Top"                                 -> top_ty
     | "(" func_ty ")"      

%import common.WS
%import common.C_COMMENT
%import common.CPP_COMMENT
%import common.CNAME
%ignore WS
%ignore C_COMMENT
%ignore CPP_COMMENT
