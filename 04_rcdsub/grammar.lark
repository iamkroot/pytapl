start: (command ";")*

?command: term
        | CNAME ":" func_ty                  -> bind

?term: "lambda" CNAME ":" func_ty "." term   -> abs
     | "if" term "then" term "else" term     -> if_stmt
     | app_term

?app_term: path_term
         | path_term app_term                -> app

?path_term: path_term "." CNAME              -> proj
          | aterm

?aterm: "(" term ")"
      | "true"                               -> true
      | "false"                              -> false
      | CNAME                                -> var
      | "{" rcd_fields "}"                 

rcd_fields: (rcd_field ",")* rcd_field       -> record

rcd_field: CNAME "=" term

?func_ty: type "->" func_ty                  -> arr_ty 
        | "{" field_types "}"
        | type

field_types: (field_ty ",")* field_ty        -> rcd_ty

field_ty: CNAME ":" func_ty

?type: "Top"                                 -> top_ty
     | "Bool"                                -> bool_ty
     | "(" func_ty ")"      

%import common.WS
%import common.C_COMMENT
%import common.CPP_COMMENT
%import common.CNAME
%import common.SH_COMMENT
%ignore WS
%ignore C_COMMENT
%ignore CPP_COMMENT
%ignore SH_COMMENT